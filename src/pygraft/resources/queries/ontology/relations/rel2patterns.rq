#  Software Name: PyGraft-gen
#  SPDX-FileCopyrightText: Copyright (c) Orange SA
#  SPDX-License-Identifier: MIT
#
#  This software is distributed under the MIT license, the text of which is available at https://opensource.org/license/MIT/ or see the "LICENSE" file for more details.
#
#  Authors: See CONTRIBUTORS.txt
#  Software description: A RDF Knowledge Graph stochastic generation solution.
#
# FILE      : rel2patterns.rq
# AUTHOR    : Ovidiu PASCAL - Orange INNOV
# DATE      : 2025-12-15
# PURPOSE   : Retrieve ontology-local object properties and their OWL
#             characteristic types (patterns) as pairs
#             (?prop_uri -> ?pattern_uri).
#
# INTENT    :
#   Detect OWL "characteristics" (reflexive, functional, transitive, etc.)
#   for the object-property universe selected by relations.rq.
#
#   This query no longer re-implements the object-property seeding logic.
#   Instead, it consumes the universe materialized by relations.py as:
#
#       ?prop_uri rdf:type pygraft:ChosenRelation .
#
# LOGIC     :
#   1) Restrict to the precomputed object-property universe:
#        ?prop_uri a pygraft:ChosenRelation
#        - keep only IRIs (defensive)
#
#   2) Optionally bind any OWL characteristic type via a VALUES list:
#        OPTIONAL { ?prop_uri a ?pattern_uri . VALUES ?pattern_uri { ... } }
#
# OUTPUT    :
#   - ?prop_uri    : IRI of a selected object property (from relations.rq universe)
#   - ?pattern_uri : IRI of the OWL characteristic type (may be unbound)
#
# NOTES     :
#   - Pure SPARQL 1.1, no vendor extensions.
#   - Patterns are recorded only when explicitly declared in the ontology.
#   - Properties with no declared characteristics are returned with an
#     unbound ?pattern_uri.
#
# ------------------------------------------------------------------------------

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX pygraft: <http://pygraft.org/schema#>

SELECT DISTINCT
    ?prop_uri
    ?pattern_uri
WHERE {
    # 1) Precomputed object-property universe (materialized by relations.py)
    ?prop_uri a pygraft:ChosenRelation .
    FILTER (isIRI(?prop_uri))

    # 2) Optional OWL characteristic binding (explicit-only)
    OPTIONAL {
        ?prop_uri a ?pattern_uri .
        VALUES ?pattern_uri {
            owl:ReflexiveProperty
            owl:IrreflexiveProperty
            owl:SymmetricProperty
            owl:AsymmetricProperty
            owl:TransitiveProperty
            owl:FunctionalProperty
            owl:InverseFunctionalProperty
        }
    }
}
ORDER BY STR(?prop_uri)
    # NOTE:
    # ?pattern_uri is OPTIONAL and may be unbound for properties with no OWL
    # characteristics. COALESCE ensures a stable, always-defined sort key and
    # prevents RDFLib from failing when ordering rows with unbound variables.
COALESCE(STR(?pattern_uri), "")
